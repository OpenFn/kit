/**
  * Ohm grammar for interpreting our Workflow syntax
  * https://ohmjs.org/docs/intro
  */
Workflow {
  Workflow = attribute* Pair*

  attribute = "@" chained_attr_name space (attr_value | quoted_prop)

  attr_value = int | bool | (alnum | "_" | "-" | ":")+

  chained_attr_name = (alnum | "_" | "-" | ".")+

  Pair =  node (edge_with_props | edge) node

  comment = "#" (~lineTerminator any)*

  space := " " | "\t" | lineTerminator | comment

  /* lower case is important: it disables whitespace */
  node = nodeWithProps | node_name

  nodeWithProps = node_name props

  /* A node name can contain letters, numbers or underscores */
  node_name = (alnum | "_")+

  props = "(" listOf<prop, ","> ")"

  prop = alnum+ "=" propValue

  propValue = quoted_prop | bool | int | alnum+ 

  // TODO we only parse numbers as positive ints right now
  // fine for tests
  int = digit+

  bool = "false" | "true"
  
  quoted_prop = "\"" (~"\"" any)* "\""

  edge = "-"
  
  edge_with_props = "-" props "-"

  lineTerminator =  "\n" | "\r"
}

